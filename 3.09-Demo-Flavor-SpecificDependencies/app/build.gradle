apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.udacity.gradle.adsupported"
        minSdkVersion 10
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors{
        free {
            applicationId "com.udacity.gradle.adsupported.free"
        }
        paid {
            applicationId "com.udacity.gradle.adsupported.paid"
        }
    }
}

/*

As we've just seen, the Android Gradle plugin makes is very simple for us  to
declare product flavors for our app. Additionally, we've learned that  the
Android plugin creates source sets for each of our flavors, automatically.
These source sets work just like source sets for regular Java projects. In
additional to having unique source directories for each source set, we can
also assign them unique dependencies. Essentially, this means we can declare
dependencies independently for each product flavor.

Let's continue to use the free vs paid app flavor example, since it's a
pretty common one. It's is probably likely that we would enable ads in our
free version, but not in the paid version of the app. Utilizing Google's ad
services requires us to depend on the ad services library. However, we don't
want to unnecessarily bloat the paid version of our app by including a
library that won't be used. To solve this problem, we can simply declare this
dependency only for the 'free' flavor. We can do this because, since flavors
are just extra source sets, Gradle has created flavor-specific configurations
that we can assign our dependencies too. I think you can guess what these
configurations are prefixed with.

*/

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.1.1'
    freeCompile 'com.google.android.gms:play-services:7.0.0'
}

/*

Since the ad services library will be a compile-time dependency, we add it to
the 'freeCompile' configuration. We could just as well add flavor-specific
test or runtime dependencies as well. Also, we don't have to repeat our
default dependencies. Dependencies that are not assigned to a flavor-specific
configuration are available in all product flavors.

*/
